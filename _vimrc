"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => General
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Sets how many lines of history VIM has to remember
set history=1000

" Enable filetype plugins
filetype plugin indent on

" Set to auto read when a file is changed from the outside
set autoread

" With a map leader it's possible to do extra key combinations
" like <leader>w saves the current file
let mapleader = ","
let g:mapleader = ","

" Fast saving
nmap <leader>w :w!<cr>

" Copy to system clipboard
nmap <leader>y "+yy
nmap <leader>p "+pp
nmap <leader>P "+P
vmap <leader>y "+y
vmap <leader>p "+P

" Manage Plugins
execute pathogen#infect()

" In my os I like to swap capslock and escape

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => VIM user interface
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Set 7 lines to the cursor - when moving vertically using j/k
" This keeps the cursor more or less centered in the screen
set so=10

" Turn on the WiLd menu
set wildmenu
" Auto compleet the wild menu to the longest common command
set wildmode=list:longest,full

" Ignore compiled files
set wildignore=*.o,*~,*.pyc

" Always show current position
set ruler

" Height of the command bar
set cmdheight=1

" A buffer becomes hidden when it is abandoned
set hid

" Configure backspace so it acts as it should act
set backspace=eol,start,indent
set whichwrap+=<,>,h,l

" Ignore case when searching
set ignorecase

" When searching try to be smart about cases 
set smartcase

" Highlight search results
set hlsearch

" Makes search act like search in modern browsers
set incsearch

" Don't redraw while executing macros (good performance config)
set lazyredraw

" For regular expressions turn magic on
set magic

" Show matching brackets when text indicator is over them
set showmatch
" How many tenths of a second to blink when matching brackets
set mat=2

" No annoying sound on errors
set noerrorbells
set novisualbell
set t_vb=
set tm=500

" Set relative line numbers on
set relativenumber
" Set the current line to be an absolute number
set number

" Set a number toggle command to control+n
function! NumberToggle()
  if(&relativenumber == 1)
    " Toggle between everything being absolute numbers
    set norelativenumber
  else
    " Toggle bettewen everything being relative numbers
    set relativenumber
  endif
endfunc

" Set control+n to the keybinding to our function
" nnoremap <C-n> :call NumberToggle()<cr>

" Remove menu bars, toolbars, and scrollbars in gvim
set guioptions-=m
set guioptions-=T
set guioptions-=r
set guioptions-=L

" Scroll wheel moves screen up and dwon, not cursor
set mouse=a

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Colors and Fonts
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Enable syntax highlighting
syntax enable
syntax on

set t_Co=256
" hybrid colorscheme
colorscheme hybrid
set background=dark

" Set extra options when running in GUI mode
if has("gui_running")
    set guioptions-=T
    set guioptions+=e
    set t_Co=256
    set guitablabel=%M\ %t
endif

" Set utf8 as standard encoding and en_US as the standard language
set encoding=utf8

" Use Unix as the standard file type
set ffs=unix,dos,mac

" Set the default font
set guifont=Source\ Code\ Pro\ for\ Powerline:h12:cANSI

" Change the color of the line number column
" terminal mode only
"hi LineNr ctermfg=darkgrey
" gui mode only
highlight LineNr guifg=#aaaaaa
highlight LineNr guibg=#222222

" Highlight trailing whitespace
highlight ExtraWhitespace ctermbg=darkgrey guibg=#555555
match ExtraWhitespace /\s\+$/

" change between relative and absolute line numbers
set number

augroup numbertoggle
  autocmd!
  autocmd BufEnter,FocusGained,InsertLeave,WinEnter * if &nu && mode() != "i" | set rnu   | endif
  autocmd BufLeave,FocusLost,InsertEnter,WinLeave   * if &nu                  | set nornu | endif
augroup END

" Function to increase the font size by 1
function! FontSizePlus ()
    let l:gf_size_whole = matchstr(&guifont, ':h\zs\(\d\+\)\ze:cANSI$')
    let l:gf_size_whole = l:gf_size_whole + 1
    let l:new_font_size = ':h'.l:gf_size_whole.':cANSI'
    let &guifont = substitute(&guifont, ':h\d\+:cANSI$', l:new_font_size, '')
endfunction

" Function to decrease the font size by 1
function! FontSizeMinus ()
    let l:gf_size_whole = matchstr(&guifont, ':h\zs\(\d\+\)\ze:cANSI$')
    let l:gf_size_whole = l:gf_size_whole - 1
    let l:new_font_size = ':h'.l:gf_size_whole.':cANSI'
    let &guifont = substitute(&guifont, ':h\d\+:cANSI$', l:new_font_size, '')
endfunction

" Function to reset the font size to 12
function! FontSizeReset ()
    let l:new_font_size = ':h12:cANSI'
    let &guifont = substitute(&guifont, ':h\d\+:cANSI$', l:new_font_size, '')
endfunction

" I tend to find the need to show off code, changing the font size on the fly
" is very helpful when presenting
if has("gui_running")
    nmap <C-kPlus> :call FontSizePlus()<CR>
    nmap <C-kMinus> :call FontSizeMinus()<CR>
    nmap <C-k0> :call FontSizeReset()<CR>
endif

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Files, backups and undo
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Turn backup off, since most stuff is in SVN, git et.c anyway...
set nobackup
set nowb
set noswapfile

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Text, tab and indent related
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Use spaces instead of tabs
set expandtab

" Be smart when using tabs ;)
set smarttab

" 1 tab == 4 spaces
set shiftwidth=4
set tabstop=4

" Linebreak on 500 characters
" set lbr
" set tw=500

set ai "Auto indent
set cindent "cleverindent
set wrap "Wrap lines

""""""""""""""""""""""""""""""
" => Visual mode related
""""""""""""""""""""""""""""""
" Visual mode pressing * or # searches for the current selection
" Super useful! From an idea by Michael Naumann
vnoremap <silent> * :call VisualSelection('f')<CR>
vnoremap <silent> # :call VisualSelection('b')<CR>

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Moving around, tabs, windows and buffers
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Treat long lines as break lines (useful when moving around in them)
map j gj
map k gk

" Disable highlight when <leader><cr> is pressed
map <silent> <leader><cr> :noh<cr>

" Smart way to move between windows
map <C-j> :tabp<cr>
map <C-k> :tabn<cr>

" Ways to move tabs to other windows
map <C-h> :call MoveToPrevTab()<cr>
map <C-l> :call MoveToNextTab()<cr>

" Close the current buffer
map <leader>bd :Bclose<cr>

" Close all the buffers
map <leader>ba :1,1000 bd!<cr>

" Useful mappings for managing tabs
map <leader>tn :tabnew<cr>
map <leader>to :tabonly<cr>
map <leader>tc :tabclose<cr>
map <leader>tm :tabmove

" Opens a new tab with the current buffer's path
" Super useful when editing files in the same directory
map <leader>te :tabedit <c-r>=expand("%:p:h")<cr>/

" Switch CWD to the directory of the open buffer
map <leader>cd :cd %:p:h<cr>:pwd<cr>

" Specify the behavior when switching between buffers 
try
  set switchbuf=useopen,usetab,newtab
  set stal=2
catch
endtry

" Return to last edit position when opening files (You want this!)
autocmd BufReadPost *
     \ if line("'\"") > 0 && line("'\"") <= line("$") |
     \   exe "normal! g`\"" |
     \ endif
" Remember info about open buffers on close
set viminfo^=%

" Merge a tab with another
function! MoveToPrevTab()
  "there is only one window
  if tabpagenr('$') == 1 && winnr('$') == 1
    return
  endif
  "preparing new window
  let l:tab_nr = tabpagenr('$')
  let l:cur_buf = bufnr('%')
  if tabpagenr() != 1
    close!
    if l:tab_nr == tabpagenr('$')
      tabprev
    endif
    sp
  else
    close!
    exe "0tabnew"
  endif
  "opening current buffer in new window
  exe "b".l:cur_buf
endfunction

function! MoveToNextTab()
  "there is only one window
  if tabpagenr('$') == 1 && winnr('$') == 1
    return
  endif
  "preparing new window
  let l:tab_nr = tabpagenr('$')
  let l:cur_buf = bufnr('%')
  if tabpagenr() < tab_nr
    close!
    if l:tab_nr == tabpagenr('$')
      tabnext
    endif
    sp
  else
    close!
    tabnew
  endif
  "opening current buffer in new window
  exe "b".l:cur_buf
endfunction

""""""""""""""""""""""""""""""
" => Status line
""""""""""""""""""""""""""""""
" Always show the status line
set laststatus=2

" Format the status line
set statusline=\ %{HasPaste()}%F%m%r%h\ %w\ \ CWD:\ %r%{getcwd()}%h\ \ \ Line:%-14.(%l,%c%V%)\ %P


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Editing mappings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Remap VIM 0 to first non-blank character
nnoremap 0 ^
nnoremap ^ 0

" Move a line of text using ALT+[jk] or Comamnd+[jk] on mac
nmap <M-j> mz:m+<cr>`z
nmap <M-k> mz:m-2<cr>`z
vmap <M-j> :m'>+<cr>`<my`>mzgv`yo`z
vmap <M-k> :m'<-2<cr>`>my`<mzgv`yo`z

if has("mac") || has("macunix")
  nmap <D-j> <M-j>
  nmap <D-k> <M-k>
  vmap <D-j> <M-j>
  vmap <D-k> <M-k>
endif

" Delete trailing white space on save, useful for Python and CoffeeScript ;)
func! DeleteTrailingWS()
  exe "normal mz"
  %s/\s\+$//ge
  exe "normal `z"
endfunc
autocmd BufWrite *.py :call DeleteTrailingWS()
autocmd BufWrite *.coffee :call DeleteTrailingWS()


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => vimgrep searching and cope displaying
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" When you press gv you vimgrep after the selected text
vnoremap <silent> gv :call VisualSelection('gv')<CR>

" Open vimgrep and put the cursor in the right position
map <leader>g :vimgrep // **/*.<left><left><left><left><left><left><left>

" Vimgreps in the current file
map <leader><space> :vimgrep // <C-R>%<C-A><right><right><right><right><right><right><right><right><right>

" When you press <leader>r you can search and replace the selected text
vnoremap <silent> <leader>r :call VisualSelection('replace')<CR>

" grep sometimes skips the filename this will confuse latex-suite.
" Set the grap program to always generate a file-name
set grepprg=grep\ -nH\ $*

" Do :help cope if you are unsure what cope is. It's super useful!
"
" When you search with vimgrep, display your results in cope by doing:
"   <leader>cc
"
" To go to the next search result do:
"   <leader>n
"
" To go to the previous search results do:
"   <leader>p
"
map <leader>cc :botright cope<cr>
map <leader>co ggVGy:tabnew<cr>:set syntax=qf<cr>pgg
map <leader>n :cn<cr>
map <leader>r :cp<cr>


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Spell checking
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" set the spellcheck language
set spelllang=en

" set the location of the spellfile
set spellfile="$VIM\\vim81\\spell\\en.utf-8.add"

" Pressing ,ss will toggle and untoggle spell checking
map <leader>ss :setlocal spell!<cr>

" Shortcuts using <leader>
map <leader>sn ]s
map <leader>sp [s
map <leader>sa zg
map <leader>s? z=


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Misc
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Remove the Windows ^M - when the encodings gets messed up
noremap <Leader>m mmHmt:%s/<C-V><cr>//ge<cr>'tzt'm

" Quickly open a buffer for scripbble
map <leader>q :e ~/buffer<cr>

" Toggle paste mode on and off
map <leader>pp :setlocal paste!<cr>

" set so latex can be called directly
" set shellslash

" empty tex files by default load as plaintext.
" I want all tex files to load vim-latex-suite.
let g:tex_flavor='latex'

"" start with no folds
autocmd BufWinEnter * silent! :%foldopen!
set foldmethod=syntax


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Helper functions
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
function! CmdLine(str)
    exe "menu Foo.Bar :" . a:str
    emenu Foo.Bar
    unmenu Foo
endfunction

function! VisualSelection(direction) range
    let l:saved_reg = @"
    execute "normal! vgvy"

    let l:pattern = escape(@", '\\/.*$^~[]')
    let l:pattern = substitute(l:pattern, "\n$", "", "")

    if a:direction == 'b'
        execute "normal ?" . l:pattern . "^M"
    elseif a:direction == 'gv'
        call CmdLine("vimgrep " . '/'. l:pattern . '/' . ' **/*.')
    elseif a:direction == 'replace'
        call CmdLine("%s" . '/'. l:pattern . '/')
    elseif a:direction == 'f'
        execute "normal /" . l:pattern . "^M"
    endif

    let @/ = l:pattern
    let @" = l:saved_reg
endfunction


" Returns true if paste mode is enabled
function! HasPaste()
    if &paste
        return 'PASTE MODE  '
    en
    return ''
endfunction

" Don't close window, when deleting a buffer
command! Bclose call <SID>BufcloseCloseIt()
function! <SID>BufcloseCloseIt()
   let l:currentBufNum = bufnr("%")
   let l:alternateBufNum = bufnr("#")

   if buflisted(l:alternateBufNum)
     buffer #
   else
     bnext
   endif

   if bufnr("%") == l:currentBufNum
     new
   endif

   if buflisted(l:currentBufNum)
     execute("bdelete! ".l:currentBufNum)
   endif
endfunction

""""""""""""""""""""""""""""""
" => Plugin Configurations
""""""""""""""""""""""""""""""

" => md img paste
"""""""""""""""""

autocmd FileType markdown nmap <buffer><silent> <leader>i :call mdip#MarkdownClipboardImage()<CR>
" there are some defaults for image directory and image name, you can change them
" let g:mdip_imgdir = 'img'
" let g:mdip_imgname = 'image'

" => vim-biblegateway
"""""""""""""""""""""
let g:BiblegatewayVersion = "NLT"
let g:BiblegatewayNumbers = 0
let g:BiblegatewayAscii = 0

nnoremap <leader>b y :call bible#InsertBible()<CR>
vnoremap <leader>b y :call bible#InsertBible(@")<CR>
vnoremap <leader>bn y :call bible#InsertBible(@", "NLT", 1, 0)<CR>

" add C:\Program Files\Vim\vim82\bundle\vim-bible-master\diatheke to path
" install diatheke into folder above from https://wiki.crosswire.org/Frontends:Diatheke
" working link as of may2022: ftp://ftp.crosswire.org/pub/sword/frontend/diatheke/diatheke-4.0-win32.zip
" Add git bash stuff to path for sed and whatnot:
" C:\Users\<user>\AppData\Local\Programs\Git\usr\bin

" => vimwiki
""""""""""""
set nocompatible
filetype plugin on

let g:vimwiki_list = [{'path': 'C:\Users\smiers\Nextcloud\WorkBackup\BCT\Wiki',
                      \ 'syntax': 'markdown', 'ext': '.md'},
                      \ {'path': 'C:\Users\smiers\Nextcloud\PersonalKeep\Notes',
                      \ 'syntax': 'markdown', 'ext': '.md'}]


function! VimwikiSearchQuery(...) abort
    let vwquery = exists('a:1') && a:1 != "" ? a:1 : input("Query: ")
    execute 'VimwikiSearch /' . vwquery '/'
endfunction

" open a default file of the wiki on vim startup
au VimEnter * if eval("@%") == "" | e C:\Users\smiers\Nextcloud\WorkBackup\BCT\Wiki\index.md | endif

" search wiki
nnoremap <leader>wf :call VimwikiSearchQuery()<CR>

" => markdown-preview
"""""""""""""""""""""
" Open markdown files with Chrome.
autocmd BufEnter *.md exe 'noremap <F5> :!start C:\\Program Files\\BraveSoftware\\Brave-Browser\\Application\\brave.exe "%:p"<CR>'

" => vim-airline
""""""""""""""""
" set the airline powerline font
let g:airline_powerline_fonts = 1

" => vim-gitgutter
""""""""""""""""""
" Set the path to git
" let g:gitgutter_git_executable = 'C:\Program Files\Git\bin\git.exe'

" => vim-fugitive
"""""""""""""""""

" => surround
"""""""""""""

" => nerdtree
"""""""""""""
" Start nerdtree up when no file is specified
autocmd StdinReadPre * let s:std_in=1
autocmd VimEnter * if argc() == 0 && !exists("s:std_in") | NERDTree | endif
" Start nerdtree up when a directory is specified
autocmd StdinReadPre * let s:std_in=1
autocmd VimEnter * if argc() == 1 && isdirectory(argv()[0]) && !exists("s:std_in") | exe 'NERDTree' argv()[0] | wincmd p | ene | exe 'cd '.argv()[0] | endif
" toggle on off nerdtree
map <C-n> :NERDTreeToggle<CR>
" close vim if nerdtree is the only thing left
autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif

" => vim-ale
""""""""""""
" linter used primarily for VHDL/Verilog
let b:ale_fixers = ['remove_trailing_lines', 'trim_whitespace']
let b:ale_linters = ['xvlog', 'xvhdl']
let g:ale_linters_explicit = 1
let g:ale_lint_on_text_changed = 'never'
let g:ale_lint_on_insert_leave = 0
let g:ale_lint_on_enter = 0
let g:ale_vhdl_xvhdl_executable = 'C:\Xilinx\Vivado\2016.2\bin\xvhdl.bat'

" => syntastic
""""""""""""""
" Linter that I primarily use for Python
let g:syntastic_always_populate_loc_list = 1
let g:syntastic_auto_loc_list = 1
let g:syntastic_check_on_open = 1
let g:syntastic_check_on_wq = 0
" use both flake8 and pylint for a python checker
let g:syntastic_mode_map = { 'mode': 'passive',     
                          \ 'active_filetypes': [],     
                          \ 'passive_filetypes': [] } 
let g:syntastic_auto_loc_list=1     
" remap the F7 key to do a check
nnoremap <silent> <F7> :SyntasticCheck<CR>  
" what should syntastic use to check python
let g:syntastic_python_checkers = ['flake8', 'pylint']

" set python path
" let $PYTHONPATH = "C:\\Python27\\Lib;C:\\Python27\\DLLs;C:\\Python27\\Lib\\lib-tk

" let $PYTHONPATH = "C:\Program Files\Python\Python310\Lib;C:\Program Files\Python\Python310\DLLs"


" => youCompleteMe
""""""""""""""""""
" C:\Program Files\Python\Python310\python310.dll
" C:\Program Files\Python\Python310
" C:\Program Files\Python\Python310\python.exe

" set pythonthreedll="C:\Program Files\Python\Python310\python310.dll"
" set pythonthreehome="C:\Program Files\Python\Python310"
" let g:ycm_python_binary_path = "C:\Programs Files\Python\Python310\python.exe"

" => nerdcommenter
""""""""""""""""""
" Add spaces after comment delimiters by default
let g:NERDSpaceDelims = 1
" Use compact syntax for prettified multi-line comments
let g:NERDCompactSexyComs = 1
" Align line-wise comment delimiters flush left instead of following code indentation
let g:NERDDefaultAlign = 'left'
" Allow commenting and inverting empty lines (useful when commenting a region)
let g:NERDCommentEmptyLines = 1
" Enable trimming of trailing whitespace when uncommenting
let g:NERDTrimTrailingWhitespace = 1
" Enable NERDCommenterToggle to check all selected lines is commented or not 
let g:NERDToggleCheckAllLines = 1

" => vim-indent-guides
""""""""""""""""""""""
" enable indent guides by default
let g:indent_guides_enable_on_vim_startup = 1

" => Tabular
""""""""""""

" => vim-snippets
"""""""""""""""""

" => SimplylFold
"""""""""""""""""
let g:SimpylFold_docstring_preview = 1
